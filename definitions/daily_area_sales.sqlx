config {
  type: "incremental",
  tags: ["daily"],
  bigquery: {
    partitionBy: "DATE(d)",
    requirePartitionFilter: true,
  },
  assertions: {
    nonNull: ["Location"],
    uniqueKey: ["Location"],
    rowConditions: [
      'd < TIMESTAMP_TRUNC(CURRENT_TIMESTAMP(), DAY)'
    ]
  }
}

SELECT
  Location as location, 
  TIMESTAMP_TRUNC(Brew_Date, DAY) d, 
  SUM(Total_Sales) AS daily_location_sales
FROM
  ${ref("brewery_partitioned_clustered")}
WHERE
  TIMESTAMP_TRUNC(Brew_Date, DAY) >= timestamp_checkpoint
GROUP BY
  location, d

pre_operations {
  DECLARE timestamp_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT MAX(d) FROM ${self()} WHERE d is not null`,
    `SELECT TIMESTAMP("2023-01-01")`)}
  )
}